# Maintainer : Philip MÃ¼ller <philm@manjaro.org>

pkgname=baloo-widgets
pkgver=5.1.0.1
_pkgver=5.1.0.1
pkgrel=1
pkgdesc="Widgets for the baloo framework for searching and managing metadata"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdelibs/baloo'
license=('LGPL')
depends=('baloo-frameworks' 'kdelibs4support' 'kemoticons')
makedepends=('extra-cmake-modules' 'kdoctools' 'git')
conflicts=('baloo4-widgets')
#source=("http://download.kde.org/unstable/plasma/$pkgver/src/${_pkgname}-$pkgver.tar.xz")
source=("${pkgname}::git://anongit.kde.org/baloo-widgets"
        "filefetch.patch::https://projects.kde.org/projects/kde/kdelibs/baloo-widgets/repository/revisions/990f4b32fc8d717379607431a7e7d7d31df28215/diff.diff")
md5sums=('SKIP' 'SKIP')

pkgver() {
  cd ${pkgname}
  printf "${pkgver}.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
    cd ${pkgname}
    patch -Rp1 -i ${srcdir}/filefetch.patch
    cd ..
    mkdir -p build
}

build() {
  cd build
  
  cmake ../${pkgname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install
}

