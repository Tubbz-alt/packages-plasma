# Contributor: Antonio Rojas

pkgname=milou
pkgver=5.0.95
pkgrel=1
pkgdesc="A dedicated search application built on top of Baloo"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/milou/'
license=('LGPL')
depends=('krunner')
provides=('milou-frameworks')
conflicts=('milou-frameworks')
replaces=('milou-frameworks')
makedepends=('extra-cmake-modules' 'kdoctools')
source=("http://download.kde.org/unstable/plasma/$pkgver/$pkgname-$pkgver.tar.xz")
sha256sums=('42eeee0e528d8c943d0335c704da5c113a4c8dd4ed28690aa0a684ee9bbbf3a8')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DQML_INSTALL_DIR=lib/qt/qml \
    -DLOCALE_INSTALL_DIR=share/locale/kf5
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}