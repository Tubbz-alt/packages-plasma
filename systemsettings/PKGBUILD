# Contributor: Antonio Rojas

pkgname=systemsettings
pkgver=5.0.95
pkgrel=1
pkgdesc='KDE system settings'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/systemsettings'
license=('LGPL')
depends=('kcmutils' 'khtml' 'kdelibs4support')
makedepends=('extra-cmake-modules' 'kdoctools')
conflicts=('kdebase-workspace')
source=("http://download.kde.org/unstable/plasma/$pkgver/$pkgname-$pkgver.tar.xz")
sha256sums=('8ee147b647da4bb80413d397fda5ce54bf3e2eef78a3822f6ec13ee0c1b37909')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DLOCALE_INSTALL_DIR=share/locale/kf5 \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
