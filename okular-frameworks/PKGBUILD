# Maintainer: Antonio Rojas 

pkgname=okular-frameworks
pkgver=r6908.98eb283
_pkgver=4.12.2
pkgrel=2
pkgdesc='Universal document viewer'
arch=('i686' 'x86_64')
url='http://kde.org/applications/graphics/okular/'
license=('GPL')
depends=('kpty' 'kdelibs4support' 'threadweaver' 'kactivities-frameworks' 'khtml' 'chmlib' 'djvulibre' 'libspectre' 'poppler-qt5' 
'libkscreen-frameworks')
makedepends=('extra-cmake-modules' 'kdoctools' 'git' 'python')
conflicts=('kdegraphics-okular' 'okular' 'kf5-okular')
provides=('okular')
replaces=('kf5-okular')
source=("git://anongit.kde.org/okular.git#branch=frameworks")
install=$pkgname.install
sha256sums=('SKIP')

pkgver() {
  cd okular
  printf "${pkgver}.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() { 
  cd build
  cmake ../okular \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
