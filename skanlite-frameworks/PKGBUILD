# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=skanlite-frameworks
pkgver=r202.7e149ce
_pkgver=1.1.1
pkgrel=2
pkgdesc="A Lite image scanning application."
arch=('i686' 'x86_64')
url="http://www.kde.org/applications/graphics/skanlite"
license=('GPL')
depends=('libksane-frameworks' 'kio')
makedepends=('cmake' 'git' 'extra-cmake-modules')
conflicts=('skanlite' 'kf5-skanlite')
provides=('skanlite')
replaces=('kf5-skanlite')
source=('git://anongit.kde.org/skanlite.git#branch=frameworks')
sha1sums=('SKIP')
_gitname="skanlite"

pkgver() {
  cd "${_gitname}"
  printf "${_pkgver}.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  rm -fr build
  mkdir -p build
}

build() { 
  cd build
  cmake "../${_gitname}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
