# Maintainer : Philip MÃ¼ller <philm@manjaro.org>

pkgname=bluedevil-frameworks
_pkgname=bluedevil
pkgver=2.0rc1
_pkgver=2.0rc1
pkgrel=3
pkgdesc='KDE bluetooth framework'
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/extragear/base/bluedevil"
license=('GPL2')
depends=('bluez' 'libbluedevil-frameworks' 'kio')
makedepends=('extra-cmake-modules' 'git')
conflicts=('bluedevil' 'kf5-bluedevil')
replaces=('kf5-bluedevil')
install=bluedevil.install
source=("git://anongit.kde.org/bluedevil#branch=frameworks")
md5sums=('SKIP')

pkgver() {
  cd bluedevil
  printf "${_pkgver}.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../bluedevil \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
