# Maintainer : Philip MÃ¼ller <philm@manjaro.org>

pkgname=kf5-sddm-kcm
_pkgname=sddm-kcm
pkgver=0.9.r66.ded0c6b
pkgrel=1
pkgdesc="SDDM configuration module for KDE"
arch=('i686' 'x86_64')
url="https://github.com/sddm/sddm-kcm"
license=("GPL")
depends=('kcoreaddons' 'sddm-qt5' 'ki18n' 'kxmlgui' 'kauth' 'kdelibs4support' 'libx11' 'libxcb' 'libxcursor')
makedepends=('git' 'extra-cmake-modules' 'kdoctools' 'qt5-tools')
conflicts=('kcm-sddm')
source=('git://github.com/sddm/sddm-kcm.git#branch=qt5')
md5sums=('SKIP')

pkgver() {
  cd $_pkgname
  printf "0.9.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  mkdir -p build
  cd build
  
  cmake ../${_pkgname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}