# Maintainer: Antonio Rojas

pkgname=kscreen-frameworks
pkgver=r497.e6de52c
_pkgver=1.9.90
pkgrel=2
pkgdesc='KDE screen management software'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/extragear/libs/libkscreen'
license=('LGPL')
depends=('kxmlgui' 'libkscreen-frameworks' 'qt5-quickcontrols')
makedepends=('extra-cmake-modules' 'git' 'python')
conflicts=('kf5-kscreen' 'kscreen')
replaces=('kf5-kscreen')
source=('git://anongit.kde.org/kscreen.git#branch=frameworks')
md5sums=('SKIP')

pkgver() {
  cd kscreen
  printf "${_pkgver}.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kscreen \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
