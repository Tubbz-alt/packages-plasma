# Maintainer : Philip MÃ¼ller <philm@manjaro.org>
# Contributor: Alexey D. <lq07829icatm at rambler.ru>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kf5-konsole
pkgver=5.0.1
pkgrel=1
pkgdesc="KDE's terminal emulator"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/applications/konsole'
license=('LGPL')
depends=('kdelibs4support' 'knotifyconfig' 'kpty')
makedepends=('extra-cmake-modules' 'git' 'kdoctools')
conflicts=('kdebase-konsole')
source=('git://anongit.kde.org/konsole.git#branch=frameworks')
md5sums=('SKIP')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../konsole \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
