# Maintainer : Philip MÃ¼ller <philm@manjaro.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=baloo-frameworks
pkgver=5.0.95
pkgrel=2
pkgdesc="A framework for searching and managing metadata"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdelibs/baloo'
license=('LGPL')
depends=('xapian-core' 'kfilemetadata5' 'kidletime' 'kcmutils' 'kdelibs4support')
makedepends=('extra-cmake-modules' 'kdoctools')
conflicts=('baloo4' 'kf5-baloo')
provides=('baloo' 'kf5-baloo')
replaces=('kf5-baloo')
source=("http://download.kde.org/unstable/plasma/$pkgver/baloo-$pkgver.tar.xz")
sha256sums=('93e02507c5653d23809d62d21210ec0ae35fb651b375eac7acaea62aa8d89504')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../baloo-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DQML_INSTALL_DIR=lib/qt/qml \
    -DLOCALE_INSTALL_DIR=share/locale/kf5 \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
