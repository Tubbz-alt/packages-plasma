# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: A Rojas < nqn1976 @ gmail.com >

pkgname=partitionmanager
pkgver=1.1.60
_pkgver=1.1.60
pkgrel=2
pkgdesc="A KDE utility that allows you to manage disks, partitions, and file systems"
arch=('i686' 'x86_64')
url='http://kde.org/applications/system/kdepartitionmanager/'
license=('GPL2')
depends=('parted' 'frameworkintegration' 'kinit' 'ktexteditor')
conflicts=('kf5-partitionmanager')
replaces=('kf5-partitionmanager')
makedepends=('cmake' 'git' 'extra-cmake-modules' 'kdoctools')
optdepends=('e2fsprogs: ext2/3/4 support'
            'xfsprogs: XFS support'
            'jfsutils: JFS support'
            'reiserfsprogs: Reiser support'
            'ntfsprogs: NTFS support'
            'dosfstools: FAT32 support')
source=('git://anongit.kde.org/partitionmanager')
sha1sums=('SKIP')
install=partitionmanager.install


pkgver() {
  cd "${pkgname}"
  printf "${_pkgver}.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${pkgname}"
  cmake . -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DLIB_INSTALL_DIR=lib \
          -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd "${pkgname}"
  make DESTDIR="${pkgdir}" install
}

