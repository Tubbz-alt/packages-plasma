# Maintainer: Antonio Rojas 
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: TimothÃ©e Ravier <tim@siosm.fr>

pkgname=libnm-qt5
pkgver=5.0.95
pkgrel=2
pkgdesc='Qt-only wrapper for NetworkManager DBus API'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/extragear/libs/libnm-qt'
license=('LGPL')
depends=('networkmanager' 'qt5-base')
makedepends=('extra-cmake-modules')
source=("http://download.kde.org/unstable/plasma/$pkgver/libnm-qt-$pkgver.tar.xz")
sha256sums=('5fc43235de875b7ede100965615882547182c585afa10749145ab70da15c28ab')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../libnm-qt-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
